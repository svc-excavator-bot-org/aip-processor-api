/*
 * (c) Copyright 2019 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.4.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.1.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.18.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.0.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.6.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'

version gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'

    version rootProject.version
    group 'com.palantir.aip.processors.api'

    repositories {
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:deprecation', '-Werror']
    }

    java {
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    tasks.check.dependsOn javadoc, checkUnusedDependencies
}

