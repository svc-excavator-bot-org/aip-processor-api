/*
 * (c) Copyright 2019 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.external-publish-dist'

task distTar(type: Tar) {
    from ('src/main/proto/')
    include('*')
}

dependencies {
    api 'com.google.protobuf:protobuf-java'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-stub'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.19.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.45.0'
        }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }

        all().each { generateProtoTask ->
            // The generateProto task does not seem to properly clean its previously generated outputs.
            // See https://github.com/google/protobuf-gradle-plugin/issues/332
            // See https://github.com/google/protobuf-gradle-plugin/issues/331
            generateProtoTask.doFirst {
                delete generateProtoTask.outputs
            }
        }

    }
    generatedFilesBaseDir = "${projectDir}/generated_src"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file(protobuf.generatedFilesBaseDir)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone.errorproneArgs += ['-XepDisableAllChecks']
}

tasks.idea.dependsOn 'generateProto'
